buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
}
plugins {
	id "java"
	id "net.ltgt.apt" version "0.21"
	id "org.ajoberstar.grgit" version "3.1.1"
}
repositories {
	mavenCentral()
}
sourceCompatibility = 11
targetCompatibility = 11
ext {
	buildLibsPath = "build${File.separator}libs"
	connectorSrcDir = "${project.buildDir}${File.separator}flink-connectors"
	connectorCommit = "043274b"
}
wrapper {
	gradleVersion = "4.10.3"
}
import org.ajoberstar.grgit.Grgit
task cloneConnectorRepo {
	doLast {
		def destDir = file(connectorSrcDir)
		if(destDir.exists()) {
			if(destDir.isDirectory()) {
				if(0 < destDir.list().length) {
					return
				}
			} else {
				throw new GradleException("${connectorSrcDir} is not a directory")
			}
		}
		if(!destDir.exists()) {
			def gitRepo = Grgit.clone {
				uri = "https://github.com/pravega/flink-connectors.git"
				dir = connectorSrcDir
			}
			gitRepo.reset {
				commit = connectorCommit
				mode = "hard"
			}
		}
	}
}
task connectorJar(type: Exec, dependsOn: cloneConnectorRepo) {
	workingDir connectorSrcDir
	commandLine "./gradlew", "jar"
}
compileJava.dependsOn += connectorJar
configurations.all {
	resolutionStrategy {
		force "org.projectlombok:lombok:1.18.6"
	}
}
dependencies {
	compileOnly(
		"org.projectlombok:lombok:1.18.6",
	)
}
